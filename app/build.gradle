apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "SERVER_URL", '"http://xx.xxx.com/"'
        buildConfigField "String", "WX_ID", '"234567890FGHOM"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig null //置空，文章说这里如果不置为null，打包时还是会以内置的签名打包
        }

        debug {
            signingConfig null //置空，文章说这里如果不置为null，打包时还是会以内置的签名打包
        }

    }

    //签名配置
    signingConfigs {

        sitRelease {
            storeFile file("../key/sit-key-20190127.jks")
            storePassword "lh123456"
            keyAlias "sit-key"
            keyPassword "lh123456"
        }

        prodarkRelease {
            storeFile file("../key/prodark-key-20190127.jks")
            storePassword "lh123456"
            keyAlias "prodark-key"
            keyPassword "lh123456"
        }

        productRelease {
            storeFile file("../key/product-key-20190127.jks")
            storePassword "lh123456"
            keyAlias "product-key"
            keyPassword "lh123456"
        }

    }

    flavorDimensions "default"
    productFlavors {
        //测试
        sit {
            applicationId "com.liuh.sit"
            //指定签名配置
            signingConfig signingConfigs.sitRelease
        }
        //灰度
        prodark {
            applicationId "com.liuh.prodark"
            versionCode 2
            versionName "2.0"
            //指定签名配置
            signingConfig signingConfigs.prodarkRelease
        }
        //生产
        product {
            applicationId "com.liuh.product"
            //指定签名配置
            signingConfig signingConfigs.productRelease

        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName="${variant.productFlavors[0].name}-v${variant.productFlavors[0].versionName}-${releaseTime()}.apk"
            }
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
